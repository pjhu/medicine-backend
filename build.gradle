buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.2.RELEASE'
    id 'java'
    id 'groovy'
    id "org.flywaydb.flyway" version "5.2.4"
}

repositories {
    mavenCentral()
}

ext.testsResults = []
allprojects {
    apply plugin: 'idea'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.pjhu'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = '1.8'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
        }
    }

    dependencies {

        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation "org.springframework.boot:spring-boot-starter-security"
        implementation 'org.springframework.boot:spring-boot-starter-data-redis'
        implementation 'redis.clients:jedis'

        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        implementation'com.google.guava:guava:23.5-jre'
        implementation'org.apache.commons:commons-lang3:3.8'
        implementation'com.google.code.gson:gson:2.7'

        implementation 'org.apache.poi:poi-ooxml:4.0.0'
        implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv:2.9.8'
        implementation 'commons-io:commons-io:2.6'
        implementation 'org.apache.tika:tika-core:1.20'

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'

        // spock test
        implementation "org.codehaus.groovy:groovy-all:2.5.7"
        testImplementation "org.spockframework:spock-core:1.2-groovy-2.4"
        testImplementation 'org.spockframework:spock-spring:1.2-groovy-2.4'
        testRuntimeOnly "com.h2database:h2"
    }

    // Test Logging
    test {
        testLogging {
            afterSuite { desc, result ->
                if (!desc.parent) {
                    def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                    def startItem = '|  ', endItem = '  |'
                    def repeatLength = startItem.length() + output.length() + endItem.length()
                    println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
                }
            }
        }
    }
}

bootJar.enabled = false
jar.enabled = false
